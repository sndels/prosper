#pragma shader_stage(compute)

#extension GL_EXT_shader_image_load_formatted : require

#include "../common/math.glsl"
#include "../common/random.glsl"

// Based on A Life of a Bokeh by Guillaume Abadie
// https://advances.realtimerendering.com/s2018/index.htm

layout(set = 0, binding = 0) uniform texture2D inHalfResIllumination;
layout(set = 0, binding = 1) uniform texture2D inHalfResCoC;
layout(set = 0, binding = 2) uniform readonly image2D inDilatedTileMinMaxCoC;
layout(set = 0, binding = 3) uniform writeonly image2D
    outHalfResBokehColorWeight;
layout(set = 0, binding = 4) uniform sampler nearestSampler;
layout(set = 0, binding = 5) uniform sampler bilinearSampler;

layout(push_constant) uniform GatherPC
{
    ivec2 halfResolution;
    vec2 invHalfResolution;
    uint frameIndex;
}
PC;

int octawebRingSampleCount(int ring)
{
    int ringSampleFactor = 8;
    return ring == 0 ? 1 : ringSampleFactor * ring;
}

float octawebRingSpacing(float kernelRadius, int ringCount)
{
    // Need to place n-1 rings along the radius since center is ring 0
    return kernelRadius / float(ringCount - 1);
}

float octawebRingRadius(float ring, float ringSpacing)
{
    return ring * ringSpacing;
}

float octawebRimSpacing(int ringSampleCount)
{
    return 2.0 * PI / float(ringSampleCount);
}

vec2 octawebSample(
    int ring, int s, int ringSampleCount, float ringRadius, float rimSpacing)
{
    float r = ringRadius;
    float phi = float(s) * rimSpacing;
    // Rings are 'interleaved', probably to make the pattern less obviously
    // repeating
    if (ring % 2 == 0)
        phi += rimSpacing * 0.5;

    float x = r * cos(phi);
    float y = r * sin(phi);

    return vec2(x, y);
}

#define MAX_COC_ERROR 0.05

struct BackgroundBucket
{
    vec4 colorWeightSum;
    float cocSum;
    float sampleCount;
};

BackgroundBucket blendBuckets(
    BackgroundBucket previousBucket, BackgroundBucket currentBucket,
    uint ringSampleCount)
{
    float currentOpacity =
        saturate(float(currentBucket.sampleCount) / float(ringSampleCount));

    float occludingCoC = saturate(
        (previousBucket.cocSum / previousBucket.sampleCount) -
        (currentBucket.cocSum / currentBucket.sampleCount));
    float blendFactor = (previousBucket.colorWeightSum.a == 0.)
                            ? 0.
                            : (1. - currentOpacity * occludingCoC);

    BackgroundBucket ret;
    ret.colorWeightSum = previousBucket.colorWeightSum * blendFactor +
                         currentBucket.colorWeightSum;
    ret.cocSum = previousBucket.cocSum * blendFactor + currentBucket.cocSum;
    ret.sampleCount =
        previousBucket.sampleCount * blendFactor + currentBucket.sampleCount;

    return ret;
}

#define DOF_SINGLE_PIXEL_RADIUS 0.7071 // length(vec2(0.5, 0.5))
float sampleAlpha(float sampleCoC)
{
    // Real-Time Depth-of-Field Rendering Using Point Splatting on Per-Pixel
    // Layers
    // by Lee et al.
    // and
    // Next Generation Post Processing in Call of Duty
    // Advanced Warfare by Jimenez
    // Lee has 4./ but we use the same weighing function for all so changing the
    // constant numerator should be ok like Jimenez had rcp
    return min(
        1. / (PI * sampleCoC * sampleCoC),
        1. / (PI * DOF_SINGLE_PIXEL_RADIUS * DOF_SINGLE_PIXEL_RADIUS));
}

void addBucketSample(
    vec2 sampleUv, float sampleCoC, inout BackgroundBucket bucket)
{
    // TODO:
    // Figure out how to properly combat the extra blur and noticeable boundary
    // between the depth slices that sample from different mips
    float mip = max(floor(log2(sampleCoC)) - 1, 0);
    // TODO:
    // Pre-calc alongside the CoC
    float sampleWeight = sampleAlpha(sampleCoC);
    bucket.colorWeightSum += vec4(
        textureLod(
            sampler2D(inHalfResIllumination, bilinearSampler), sampleUv, mip)
                .rgb *
            sampleWeight,
        sampleWeight);
    bucket.cocSum += sampleCoC;
    bucket.sampleCount++;
}

vec4 gatherBackground(vec2 coord, float tileMinCoC, float tileMaxCoC)
{
    // TODO:
    // Larger, smaller early out?
    // This is 2px in full res
    float minCoC = 1.;

    if (tileMaxCoC < minCoC)
        return vec4(0);

    // TODO: Fastpath?
    // - Criteria just abs(tileMaxCoC - tileMinCoC) > (1 - MAX_COC_ERROR) *
    // tileMaxCoC?
    //   - Just average over all kernel samples?
    //   - Use bilinear sampling?

    // Collect rings moving from the edges toward the center to approximate
    // occlusion. Smaller background CoCs are for surfaces that are in front of
    // surfaces that have larger ones.
    // From Abadie

    // The center sample counts as a ring, so ringCount should be at least 2
    int ringCount = 5;
    float kernelRadius = tileMaxCoC;
    float radiusJitterScale = .5;
    // float radiusJitterScale = 0.;
    float radiusJitter = rnd01() * 2. - 1.;
    radiusJitter *= radiusJitterScale;
    // Jitter radius to get rid of tile artifacts
    kernelRadius += radiusJitter;

    float ringSpacing = octawebRingSpacing(kernelRadius, ringCount);
    float spacingJitterScale = ringSpacing * .5;
    // float spacingJitterScale = 0.;

    vec2 coordJitter = rnd2d01() * 2. - 1.;
    coordJitter *= spacingJitterScale;
    coord += coordJitter;

    // TODO:
    // The Abadie slides have error=1. but it had a mention of tweaks based
    // on other factors. Probably need to account for jitter at least.
    // float cocRadiusError = (1. + radiusJitterScale + spacingJitterScale) *
    // .5;
    float cocRadiusError = 1.;

    BackgroundBucket previousBucket = BackgroundBucket(vec4(0), 0., 0.);
    for (int ring = ringCount - 1; ring >= 0; --ring)
    {
        // Different from Abadie, but feels right
        float borderingRadius =
            octawebRingRadius(float(ring) + .5 + cocRadiusError, ringSpacing);

        BackgroundBucket currentBucket = BackgroundBucket(vec4(0), 0., 0.);
        int ringSampleCount = octawebRingSampleCount(ring);
        float ringRadius = octawebRingRadius(ring, ringSpacing);
        float rimSpacing = octawebRimSpacing(ringSampleCount);
        for (int si = 0; si < ringSampleCount; ++si)
        {
            vec2 s = octawebSample(
                ring, si, ringSampleCount, ringRadius, rimSpacing);

            vec2 sampleUv = (coord + s) * PC.invHalfResolution;

            float sampleCoC =
                textureLod(sampler2D(inHalfResCoC, nearestSampler), sampleUv, 0)
                    .x;
            // Jitter not applied per sample so all samples in a given ring have
            // the same radius
            if (sampleCoC >= ringRadius)
            {
                // Sort into previous and current to approximate occlusion
                if (sampleCoC < borderingRadius)
                    addBucketSample(sampleUv, sampleCoC, currentBucket);
                else
                    addBucketSample(sampleUv, sampleCoC, previousBucket);
            }
        }

        previousBucket =
            blendBuckets(previousBucket, currentBucket, ringSampleCount);
    }

    vec3 color = previousBucket.colorWeightSum.rgb /
                 max(previousBucket.colorWeightSum.w, 0.00001);

    // Background blending will happen based on the full res pixel CoC
    return vec4(color, 0);
}

vec4 gatherForeground(vec2 coord, float tileMinCoC, float tileMaxCoC)
{
    // TODO:
    // Larger, smaller early out?
    float maxCoC = -1.;

    if (tileMinCoC > maxCoC)
        return vec4(0);

    // The center sample counts as a ring, so ringCount should be at least 2
    int ringCount = 5;
    // Foreground CoC is negative
    float kernelRadius = -tileMinCoC;
    // Jitter radius to get rid of tile artifacts
    kernelRadius *= 1 + (rnd01() * 2 - 1) * MAX_COC_ERROR;
    float ringSpacing = octawebRingSpacing(kernelRadius, ringCount);

    vec2 jitter = rnd2d01() * 2 - 1;
    jitter *= ringSpacing;
    coord += jitter;

    // TODO: Fastpath?
    // - Criteria just (tileMinCoC - tileMaxCoC) < 0.95 * tileMinCoC?
    //   - Just average over all kernel samples?
    //   - Use bilinear sampling?

    vec3 colorSum = vec3(0);
    float alphaSum = 0;
    float totalWeight = 0;
    int sampleCount = 0;
    for (int ring = 0; ring < ringCount; ++ring)
    {
        int ringSampleCount = octawebRingSampleCount(ring);
        float ringRadius = octawebRingRadius(ring, ringSpacing);
        float rimSpacing = octawebRimSpacing(ringSampleCount);
        sampleCount += ringSampleCount;
        for (int si = 0; si < ringSampleCount; ++si)
        {
            vec2 s = octawebSample(
                ring, si, ringSampleCount, ringRadius, rimSpacing);

            vec2 sampleUv = vec2(coord + s) * PC.invHalfResolution;

            // Foreground CoC is negative
            float sampleCoC =
                -textureLod(
                     sampler2D(inHalfResCoC, nearestSampler), sampleUv, 0)
                     .x;
            if (sampleCoC < -maxCoC)
                continue;

            float sampleDistance = length(s);
            if (sampleCoC >= sampleDistance - ringSpacing)
            {
                // Sousa: weigh samples with tile max CoC
                // against near CoC
                // TODO:
                // Is this what was meant? Seems to add blending between
                // 'layers' in foreground
                float sampleWeight = kernelRadius / sampleCoC;
                colorSum +=
                    textureLod(
                        sampler2D(inHalfResIllumination, nearestSampler),
                        sampleUv, 0)
                        .rgb *
                    sampleWeight;
                alphaSum += sampleAlpha(sampleCoC);
                totalWeight += sampleWeight;
            }
        }
    }

    vec3 color = colorSum / max(totalWeight, 0.001);
    // From Next Generation Post Processing in Call of Duty Advanced
    // Warfare
    // by Jimenez
    // Bake in sample count normalization
    // 2. moves blend start outward from objects, avoiding background
    // reconstruction
    float weight = saturate(
        2. * (1. / sampleCount) * (1. / sampleAlpha(kernelRadius)) * alphaSum);

    return vec4(color, weight);
}

layout(local_size_x = GROUP_X, local_size_y = GROUP_Y) in;
void main()
{
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(coord, PC.halfResolution)))
        return;

    pcg_state = uvec3(coord, 0);

    ivec2 tileCoord = coord / 8;
    vec2 tileMinMaxCoC = imageLoad(inDilatedTileMinMaxCoC, tileCoord).xy;
    float tileMinCoC = tileMinMaxCoC[0];
    float tileMaxCoC = tileMinMaxCoC[1];

    vec2 coordf = vec2(coord) + 0.5;

    vec4 outColorWeight = vec4(0);
#ifdef GATHER_BACKGROUND
    outColorWeight = gatherBackground(coordf, tileMinCoC, tileMaxCoC);
#else  // !GATHER_BACKGROUND
    outColorWeight = gatherForeground(coordf, tileMinCoC, tileMaxCoC);
#endif // GATHER_BACKGROUND

    imageStore(outHalfResBokehColorWeight, coord, outColorWeight);
}
