#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_shader_image_load_formatted : require
#extension GL_EXT_shader_16bit_storage : require
#extension GL_EXT_shader_8bit_storage : require

#pragma shader_stage(raygen)

#include "../../brdf.glsl"
#include "../../common/random.glsl"
#include "../../common/sampling.glsl"
#include "../../debug.glsl"
#include "../../restir_di/reservoir.glsl"
#include "../../scene/camera.glsl"
#include "../../scene/geometry.glsl"
#include "../../scene/instances.glsl"
#include "../../scene/lighting.glsl"
#include "../../scene/materials.glsl"
#include "../../scene/skybox.glsl"
#include "../../shared/shader_structs/push_constants/restir_di/trace.h"
#include "../payload.glsl"
#include "../ray.glsl"
#include "../rt_data.glsl"

layout(push_constant) uniform PushConstant { TracePC PC; };

bool flagSkipHistory() { return bitfieldExtract(PC.flags, 0, 1) == 1; }
bool flagAccumulate() { return bitfieldExtract(PC.flags, 1, 1) == 1; }

layout(set = STORAGE_SET, binding = 0) uniform readonly image2D
    inAlbedoRoughness;
layout(set = STORAGE_SET, binding = 1) uniform readonly image2D
    inNormalMetallic;
layout(set = STORAGE_SET, binding = 2) uniform texture2D inNonLinearDepth;
layout(set = STORAGE_SET, binding = 3) uniform readonly image2D inReservoirs;
layout(set = STORAGE_SET, binding = 4) uniform readonly image2D
    previousDiffuseIllumination;
layout(set = STORAGE_SET, binding = 5) uniform readonly image2D
    previousSpecularIllumination;
layout(set = STORAGE_SET, binding = 6) uniform image2D outDiffuseIllumination;
layout(set = STORAGE_SET, binding = 7) uniform image2D outSpecularIllumination;
layout(set = STORAGE_SET, binding = 8) uniform sampler depthSampler;

layout(location = 0) rayPayloadEXT RayPayload payload;

#define MISS_INDEX 0xFFFFFFFF

float shadow(vec3 p, vec3 l, float tMin, float lDist)
{
    const uint mask = 0xFF;
    const uint flags = gl_RayFlagsTerminateOnFirstHitEXT;

    payload.drawInstanceIndex = MISS_INDEX;
    payload.randomSeed = pcg(pcg_state.x ^ pcg_state.y);

    traceRayEXT(as, flags, mask, 0, 0, 0, p, tMin, l, lDist, 0);

    return payload.drawInstanceIndex == MISS_INDEX ? 1. : 0.;
}

vec3 evaluateDirectLighting(VisibleSurface surface)
{
    // Alpha=0 hits don't have a valid surface
    if (surface.material.alpha == 0)
        return vec3(0);

    // Uniform sampling adapted from Physically Based Rendering 3rd ed.
    // https://pbr-book.org/3ed-2018/Light_Transport_I_Surface_Reflection/Direct_Lighting

    // Sun and others
    uint lightCount = 1 + pointLights.count + spotLights.count;
    uint lightIndex = min(uint(rnd01() * lightCount), lightCount - 1);

    vec3 l;
    float d;
    vec3 irradiance;
    sampleLight(surface, lightIndex, l, d, irradiance);

    // Don't bother evaluating backface lighting
    if (dot(l, surface.normalWS) <= 0)
        return vec3(0);

    irradiance *= shadow(surface.positionWS, l, 0.1, d);

    // Account for sampling the lights uniformly
    irradiance *= lightCount;

    return irradiance * evalBRDFTimesNoL(l, surface);
}

void evaluateDirectLightingReSTIR(
    VisibleSurface surface, LightReservoir reservoir, inout vec3 outDiffuse,
    inout vec3 outSpecular)
{
    // Alpha=0 hits don't have a valid surface
    if (surface.material.alpha == 0 || reservoir.lightIndex < 0)
    {
        outDiffuse = vec3(0);
        outSpecular = vec3(0);
        return;
    }

    vec3 l;
    float d;
    vec3 irradiance;
    sampleLight(surface, reservoir.lightIndex, l, d, irradiance);

    // Don't bother evaluating backface lighting
    if (dot(l, surface.normalWS) <= 0)
    {
        outDiffuse = vec3(0);
        outSpecular = vec3(0);
        return;
    }

    irradiance *= shadow(surface.positionWS, l, 0.1, d);

    evalBRDFTimesNoL(l, surface, outDiffuse, outSpecular);
    outDiffuse *= irradiance * reservoir.unbiasedContributionWeight;
    outSpecular *= irradiance * reservoir.unbiasedContributionWeight;
}

void main()
{
    ivec2 px = ivec2(gl_LaunchIDEXT.xy);

    pcg_state = uvec3(px, PC.frameIndex);

    vec2 uv = vec2(px) / gl_LaunchSizeEXT.xy;

    float nonLinearDepth =
        texelFetch(sampler2D(inNonLinearDepth, depthSampler), px, 0).x;

    VisibleSurface surface;
    surface.positionWS = worldPos(uv, nonLinearDepth);
    surface.invViewRayWS = normalize(camera.eye.xyz - surface.positionWS);
    surface.material = loadFromGbuffer(px, inAlbedoRoughness, inNormalMetallic);
    surface.normalWS = surface.material.normal;
    surface.uv = vec2(0);
    surface.NoV = saturate(dot(surface.normalWS, surface.invViewRayWS));
    surface.linearDepth = linearizeDepth(nonLinearDepth);

    if (PC.drawType != DrawType_Default)
    {
        if (PC.drawType == DrawType_Position)
        {
            DebugInputs di;
            di.position = surface.positionWS;
            imageStore(
                outDiffuseIllumination, px,
                vec4(commonDebugDraw(PC.drawType, di, surface.material), 1));
            imageStore(outSpecularIllumination, px, vec4(0));
        }
        else
        {
            // GBuffer pass stores debug output in albedo
            imageStore(
                outDiffuseIllumination, px, vec4(surface.material.albedo, 1));
            imageStore(outSpecularIllumination, px, vec4(0));
        }
        return;
    }

    // vec3 color = evaluateDirectLighting(surface);
    LightReservoir reservoir = unpackReservoir(imageLoad(inReservoirs, px).xy);
    vec3 diffuse, specular;
    evaluateDirectLightingReSTIR(surface, reservoir, diffuse, specular);

    float diffuseHistoryCount = 1;
    float specularHistoryCount = 1;
    if (!flagSkipHistory() && flagAccumulate())
    {
        vec4 diffuseHistory = imageLoad(previousDiffuseIllumination, ivec2(px));
        vec4 specularHistory =
            imageLoad(previousSpecularIllumination, ivec2(px));

        vec3 diffuseHistoryColor = diffuseHistory.rgb;
        vec3 specularHistoryColor = specularHistory.rgb;
        diffuseHistoryCount += diffuseHistory.a;
        specularHistoryCount += specularHistory.a;

        diffuse = diffuseHistoryColor +
                  (diffuse - diffuseHistoryColor) / diffuseHistoryCount;

        specular = specularHistoryColor +
                   (specular - specularHistoryColor) / specularHistoryCount;
    }
    imageStore(
        outDiffuseIllumination, ivec2(px), vec4(diffuse, diffuseHistoryCount));
    imageStore(
        outSpecularIllumination, ivec2(px),
        vec4(specular, specularHistoryCount));
}
